#
# Copyright (c) 2015-2020 CNRS INRIA
# Copyright (c) 2015 Wandercraft, 86 rue de Paris 91400 Orsay, France.
#

# ----------------------------------------------------
# --- C++ --------------------------------------------
# ----------------------------------------------------

SET(${PROJECT_NAME}_SOURCES
  parsers/urdf/model.cpp
  parsers/urdf/geometry.cpp
)

# Extract the compile definitions of the project for export
GET_DIRECTORY_PROPERTY(CURRENT_COMPILE_DEFINITIONS COMPILE_DEFINITIONS)

IF(UNIX)
    # Create target libpinocchio.so
    ADD_LIBRARY(${PROJECT_NAME} SHARED ${HEADERS} ${${PROJECT_NAME}_SOURCES})
    SET_TARGET_PROPERTIES(${PROJECT_NAME} PROPERTIES LINKER_LANGUAGE CXX)
    TARGET_COMPILE_DEFINITIONS(${PROJECT_NAME} PUBLIC ${CURRENT_COMPILE_DEFINITIONS})

    SET_TARGET_PROPERTIES(${PROJECT_NAME} PROPERTIES VERSION ${PROJECT_VERSION})
    IF(BUILD_WITH_COMMIT_VERSION)
      TAG_LIBRARY_VERSION(${PROJECT_NAME})
    ENDIF(BUILD_WITH_COMMIT_VERSION)

    PKG_CONFIG_USE_DEPENDENCY(${PROJECT_NAME} eigen3)
    TARGET_LINK_LIBRARIES(${PROJECT_NAME} PUBLIC ${Boost_FILESYSTEM_LIBRARY} ${Boost_SYSTEM_LIBRARY} ${Boost_SERIALIZATION_LIBRARY})
    TARGET_INCLUDE_DIRECTORIES(${PROJECT_NAME} PUBLIC $<INSTALL_INTERFACE:include>)

    # Special care of urdfdom version
    IF(URDFDOM_FOUND)
      IF(${URDFDOM_VERSION} VERSION_LESS "0.3.0")
        TARGET_COMPILE_DEFINITIONS(${PROJECT_NAME} PRIVATE -DPINOCCHIO_URDFDOM_COLLISION_WITH_GROUP_NAME)
      ENDIF(${URDFDOM_VERSION} VERSION_LESS "0.3.0")

      # defines types from version 0.4.0
      IF(NOT ${URDFDOM_VERSION} VERSION_LESS "0.4.0")
        TARGET_COMPILE_DEFINITIONS(${PROJECT_NAME} PRIVATE -DPINOCCHIO_URDFDOM_TYPEDEF_SHARED_PTR)
      ENDIF(NOT ${URDFDOM_VERSION} VERSION_LESS "0.4.0")

      # std::shared_ptr appears from version 1.0.0
      IF(${URDFDOM_VERSION} VERSION_GREATER "0.4.2")
        TARGET_COMPILE_DEFINITIONS(${PROJECT_NAME} PRIVATE -DPINOCCHIO_URDFDOM_USE_STD_SHARED_PTR)
      ENDIF(${URDFDOM_VERSION} VERSION_GREATER "0.4.2")

      PKG_CONFIG_USE_DEPENDENCY(${PROJECT_NAME} urdfdom)
    ENDIF(URDFDOM_FOUND)

    IF(HPP_FCL_FOUND)
      PKG_CONFIG_USE_DEPENDENCY(${PROJECT_NAME} hpp-fcl)
    ENDIF(HPP_FCL_FOUND)

    ADD_HEADER_GROUP(HEADERS)
    ADD_SOURCE_GROUP(${PROJECT_NAME}_SOURCES)

    INSTALL(TARGETS ${PROJECT_NAME}
            EXPORT ${TARGETS_EXPORT_NAME}
            DESTINATION lib)
ENDIF(UNIX)

