#
# Copyright (c) 2015-2022 CNRS INRIA
# Copyright (c) 2015 Wandercraft, 86 rue de Paris 91400 Orsay, France.
#

include(${PROJECT_SOURCE_DIR}/cmake/stubs.cmake)
set(CMAKE_MODULE_PATH
  "${PINOCCHIO_PROJECT_SOURCE_DIR}/cmake/find-external/GMP"
  "${PINOCCHIO_PROJECT_SOURCE_DIR}/cmake/find-external/MPFR"
  ${CMAKE_MODULE_PATH}
)

# --- MACROS --- #
MACRO(SYMLINK_AND_INSTALL_HEADERS HEADERS SUBPATH)
  FOREACH(header ${HEADERS})
    GET_FILENAME_COMPONENT(headerName ${header} NAME)
    GET_FILENAME_COMPONENT(headerPath ${header} PATH)
    EXECUTE_PROCESS(COMMAND ${CMAKE_COMMAND} -E ${LINK}
      ${CMAKE_CURRENT_SOURCE_DIR}/${header}
      ${${PROJECT_NAME}_BINARY_DIR}/include/${PROJECT_NAME}/${SUBPATH}/${header})

    INSTALL(FILES ${CMAKE_CURRENT_SOURCE_DIR}/${header}
      DESTINATION ${CMAKE_INSTALL_PREFIX}/include/${PROJECT_NAME}/${SUBPATH}/${headerPath}
            PERMISSIONS OWNER_READ GROUP_READ WORLD_READ OWNER_WRITE)
  ENDFOREACH(header)
ENDMACRO(SYMLINK_AND_INSTALL_HEADERS HEADERS SUBPATH)

FUNCTION(REMOVE_PATH_FROM_LIST list_name path_name dest_list)
  SET(list_name_)
  FOREACH(header ${list_name})
    STRING(REGEX REPLACE "${path_name}" "" header ${header})
    LIST(APPEND list_name_ ${header})
  ENDFOREACH(header ${list_name_})
  SET(${dest_list} ${list_name_} PARENT_SCOPE)
ENDFUNCTION(REMOVE_PATH_FROM_LIST)

FUNCTION(LIST_FILTER list regular_expression dest_list)
  FOREACH(elt ${list})
    IF(${elt} MATCHES ${regular_expression})
      LIST(REMOVE_ITEM list ${elt})
    ENDIF()
  ENDFOREACH(elt ${list})
  SET(${dest_list} ${list} PARENT_SCOPE)
ENDFUNCTION(LIST_FILTER)

# --- PYTHON TARGET --- #
SET(PYWRAP ${PROJECT_NAME}_pywrap)
SET(PYWRAP ${PYWRAP} PARENT_SCOPE)

FILE(GLOB_RECURSE ${PROJECT_NAME}_PYTHON_HEADERS
     ${CMAKE_CURRENT_SOURCE_DIR}/*.hpp
     )

FILE(GLOB_RECURSE ${PROJECT_NAME}_PYTHON_SOURCES
     ${CMAKE_CURRENT_SOURCE_DIR}/*.cpp
     )

REMOVE_PATH_FROM_LIST("${${PROJECT_NAME}_PYTHON_HEADERS}" "${CMAKE_CURRENT_SOURCE_DIR}/" ${PROJECT_NAME}_PYTHON_HEADERS)
REMOVE_PATH_FROM_LIST("${${PROJECT_NAME}_PYTHON_SOURCES}" "${CMAKE_CURRENT_SOURCE_DIR}/" ${PROJECT_NAME}_PYTHON_SOURCES)
LIST_FILTER("${${PROJECT_NAME}_PYTHON_SOURCES}" "^extra" ${PROJECT_NAME}_PYTHON_SOURCES)

# Parser for Python model
IF(BUILD_PYTHON_INTERFACE_WITH_PYPY)
  LIST(REMOVE_ITEM ${PROJECT_NAME}_PYTHON_HEADERS
    parsers/python.hpp
    )

  LIST(REMOVE_ITEM ${PROJECT_NAME}_PYTHON_SOURCES
    parsers/python/model.cpp
    )
ELSE(BUILD_PYTHON_INTERFACE_WITH_PYPY)
  SET(${PROJECT_NAME}_PARSER_PYTHON_HEADERS
    parsers/python.hpp
    )
ENDIF(BUILD_PYTHON_INTERFACE_WITH_PYPY)

# Python exposition of FCL
IF(NOT BUILD_WITH_HPP_FCL_PYTHON_BINDINGS)
  LIST(REMOVE_ITEM ${PROJECT_NAME}_PYTHON_HEADERS
    multibody/fcl/transform.hpp
    )
  LIST(REMOVE_ITEM ${PROJECT_NAME}_PYTHON_SOURCES
    multibody/fcl/expose-fcl.cpp
    )
ENDIF(NOT BUILD_WITH_HPP_FCL_PYTHON_BINDINGS)

IF(NOT BUILD_WITH_OPENMP_SUPPORT)
  LIST_FILTER("${${PROJECT_NAME}_PYTHON_HEADERS}" "^multibody/pool" ${PROJECT_NAME}_PYTHON_HEADERS)
  LIST_FILTER("${${PROJECT_NAME}_PYTHON_SOURCES}" "^multibody/pool" ${PROJECT_NAME}_PYTHON_SOURCES)
  LIST_FILTER("${${PROJECT_NAME}_PYTHON_HEADERS}" "^algorithm/parallel" ${PROJECT_NAME}_PYTHON_HEADERS)
  LIST_FILTER("${${PROJECT_NAME}_PYTHON_SOURCES}" "^algorithm/parallel" ${PROJECT_NAME}_PYTHON_SOURCES)
  #LIST(FILTER ${PROJECT_NAME}_PYTHON_HEADERS EXCLUDE REGEX "^multibody/pool")
  #LIST(FILTER ${PROJECT_NAME}_PYTHON_SOURCES EXCLUDE REGEX "^multibody/pool")
  #LIST(FILTER ${PROJECT_NAME}_PYTHON_HEADERS EXCLUDE REGEX "^algorithm/parallel")
  #LIST(FILTER ${PROJECT_NAME}_PYTHON_SOURCES EXCLUDE REGEX "^algorithm/parallel")
ELSE(NOT BUILD_WITH_OPENMP_SUPPORT)
  IF(NOT BUILD_WITH_HPP_FCL_SUPPORT)
    LIST_FILTER("${${PROJECT_NAME}_PYTHON_HEADERS}" "^multibody/pool/geometry.hpp" ${PROJECT_NAME}_PYTHON_HEADERS)
    LIST_FILTER("${${PROJECT_NAME}_PYTHON_HEADERS}" "^multibody/pool/broadphase-manager.hpp" ${PROJECT_NAME}_PYTHON_HEADERS)
    LIST_FILTER("${${PROJECT_NAME}_PYTHON_HEADERS}" "^multibody/broadphase-manager.hpp" ${PROJECT_NAME}_PYTHON_HEADERS)
    LIST_FILTER("${${PROJECT_NAME}_PYTHON_SOURCES}" "^algorithm/parallel/geometry.cpp" ${PROJECT_NAME}_PYTHON_SOURCES)
    LIST_FILTER("${${PROJECT_NAME}_PYTHON_SOURCES}" "^algorithm/parallel/broadphase.cpp" ${PROJECT_NAME}_PYTHON_SOURCES)
    LIST_FILTER("${${PROJECT_NAME}_PYTHON_SOURCES}" "^algorithm/expose-broadphase.cpp" ${PROJECT_NAME}_PYTHON_SOURCES)
    LIST_FILTER("${${PROJECT_NAME}_PYTHON_SOURCES}" "^algorithm/expose-broadphase-callbacks.cpp" ${PROJECT_NAME}_PYTHON_SOURCES)
    #LIST(FILTER ${PROJECT_NAME}_PYTHON_HEADERS EXCLUDE REGEX "^multibody/pool/geometry.hpp")
    #LIST(FILTER ${PROJECT_NAME}_PYTHON_SOURCES EXCLUDE REGEX "^algorithm/parallel/geometry.cpp")
  ENDIF(NOT BUILD_WITH_HPP_FCL_SUPPORT)
ENDIF(NOT BUILD_WITH_OPENMP_SUPPORT)

LIST(APPEND HEADERS ${${PROJECT_NAME}_PYTHON_HEADERS})

# Headers of the Python bindings
MAKE_DIRECTORY("${${PROJECT_NAME}_BINARY_DIR}/include/pinocchio/bindings/python")
MAKE_DIRECTORY("${${PROJECT_NAME}_BINARY_DIR}/include/pinocchio/bindings/python/context")
MAKE_DIRECTORY("${${PROJECT_NAME}_BINARY_DIR}/include/pinocchio/bindings/python/math")
MAKE_DIRECTORY("${${PROJECT_NAME}_BINARY_DIR}/include/pinocchio/bindings/python/spatial")
MAKE_DIRECTORY("${${PROJECT_NAME}_BINARY_DIR}/include/pinocchio/bindings/python/multibody")
MAKE_DIRECTORY("${${PROJECT_NAME}_BINARY_DIR}/include/pinocchio/bindings/python/multibody/joint")
MAKE_DIRECTORY("${${PROJECT_NAME}_BINARY_DIR}/include/pinocchio/bindings/python/multibody/pool")
IF(BUILD_WITH_HPP_FCL_PYTHON_BINDINGS)
  MAKE_DIRECTORY("${${PROJECT_NAME}_BINARY_DIR}/include/pinocchio/bindings/python/multibody/fcl")
ENDIF(BUILD_WITH_HPP_FCL_PYTHON_BINDINGS)
MAKE_DIRECTORY("${${PROJECT_NAME}_BINARY_DIR}/include/pinocchio/bindings/python/parsers")
MAKE_DIRECTORY("${${PROJECT_NAME}_BINARY_DIR}/include/pinocchio/bindings/python/serialization")
MAKE_DIRECTORY("${${PROJECT_NAME}_BINARY_DIR}/include/pinocchio/bindings/python/algorithm")
  MAKE_DIRECTORY("${${PROJECT_NAME}_BINARY_DIR}/include/pinocchio/bindings/python/algorithm/constraints")
IF(BUILD_WITH_OPENMP_SUPPORT)
  MAKE_DIRECTORY("${${PROJECT_NAME}_BINARY_DIR}/include/pinocchio/bindings/python/algorithm/parallel")
ENDIF(BUILD_WITH_OPENMP_SUPPORT)
MAKE_DIRECTORY("${${PROJECT_NAME}_BINARY_DIR}/include/pinocchio/bindings/python/math/multiprecision")
MAKE_DIRECTORY("${${PROJECT_NAME}_BINARY_DIR}/include/pinocchio/bindings/python/math/multiprecision/boost")
MAKE_DIRECTORY("${${PROJECT_NAME}_BINARY_DIR}/include/pinocchio/bindings/python/utils")
SYMLINK_AND_INSTALL_HEADERS("${${PROJECT_NAME}_PYTHON_HEADERS}" "bindings/python")

# Headers of the Python parser
IF(NOT BUILD_PYTHON_INTERFACE_WITH_PYPY)
  MAKE_DIRECTORY("${${PROJECT_NAME}_BINARY_DIR}/include/pinocchio/parsers/python")
  SYMLINK_AND_INSTALL_HEADERS("${${PROJECT_NAME}_PARSER_PYTHON_HEADERS}" "")
ENDIF(NOT BUILD_PYTHON_INTERFACE_WITH_PYPY)

# --- COMPILE WRAPPER
MAKE_DIRECTORY("${${PROJECT_NAME}_BINARY_DIR}/bindings/python/${PROJECT_NAME}")

FUNCTION(PINOCCHIO_PYTHON_BINDINGS_SPECIFIC_TYPE scalar_name)
  SET(scalar_name "${scalar_name}")
  SET(PYTHON_LIB_NAME "${PYWRAP}_${scalar_name}")
  SET(EXTRA_SOURCES ${${PYTHON_LIB_NAME}_EXTRA_SOURCES})
  FILE(GLOB_RECURSE EXTRA_SOURCES
       ${CMAKE_CURRENT_SOURCE_DIR}/extra/${scalar_name}/*.cpp
     )

  REMOVE_PATH_FROM_LIST("${EXTRA_SOURCES}" "${CMAKE_CURRENT_SOURCE_DIR}/" EXTRA_SOURCES)

  SET(${PYTHON_LIB_NAME}_SOURCES ${${PROJECT_NAME}_PYTHON_SOURCES} ${EXTRA_SOURCES})
  SET(${PYTHON_LIB_NAME}_HEADERS ${${PROJECT_NAME}_PYTHON_HEADERS})

  ADD_LIBRARY(${PYTHON_LIB_NAME} SHARED ${${PYTHON_LIB_NAME}_SOURCES} ${${PYTHON_LIB_NAME}_HEADERS})
  IF(BUILD_WITH_OPENMP_SUPPORT)
    TARGET_COMPILE_OPTIONS(${PYTHON_LIB_NAME} PRIVATE ${OpenMP_CXX_FLAGS})
    TARGET_COMPILE_DEFINITIONS(${PYTHON_LIB_NAME} PRIVATE -DPINOCCHIO_PYTHON_INTERFACE_WITH_OPENMP)
    TARGET_INCLUDE_DIRECTORIES(${PYTHON_LIB_NAME} SYSTEM PRIVATE ${OpenMP_CXX_INCLUDE_DIR})
    IF(LINK_PYTHON_INTERFACE_TO_OPENMP)
      TARGET_LINK_LIBRARIES(${PYTHON_LIB_NAME} PRIVATE ${OpenMP_CXX_LIBRARIES})
    ENDIF(LINK_PYTHON_INTERFACE_TO_OPENMP)
  ENDIF(BUILD_WITH_OPENMP_SUPPORT)
  ADD_DEPENDENCIES(python ${PYTHON_LIB_NAME})

  SET_TARGET_PROPERTIES(${PYTHON_LIB_NAME} PROPERTIES PREFIX "") # Remove lib prefix for the target

  # Do not report:
  #  -Wconversion as the BOOST_PYTHON_FUNCTION_OVERLOADS implicitly converts.
  #  -Wcomment as latex equations have multi-line comments.
  IF(NOT WIN32)
    TARGET_COMPILE_OPTIONS(${PYTHON_LIB_NAME} PRIVATE -Wno-conversion -Wno-comment)
  ENDIF(NOT WIN32)
  SET(PINOCCHIO_PYTHON_CONTEXT_FILE_VALUE "pinocchio/bindings/python/context/${scalar_name}.hpp")
  TARGET_COMPILE_OPTIONS(${PYTHON_LIB_NAME} PRIVATE -DPINOCCHIO_PYTHON_CONTEXT_FILE="${PINOCCHIO_PYTHON_CONTEXT_FILE_VALUE}" -DPINOCCHIO_PYTHON_MODULE_NAME=${PYTHON_LIB_NAME})

  SET_TARGET_PROPERTIES(${PYTHON_LIB_NAME} PROPERTIES VERSION ${PROJECT_VERSION})
  IF(BUILD_WITH_COMMIT_VERSION)
    TAG_LIBRARY_VERSION(${PYTHON_LIB_NAME})
  ENDIF(BUILD_WITH_COMMIT_VERSION)
  ADD_HEADER_GROUP(${PYTHON_LIB_NAME}_HEADERS)
  ADD_SOURCE_GROUP(${PYTHON_LIB_NAME}_SOURCES)

  TARGET_LINK_LIBRARIES(${PYTHON_LIB_NAME} PUBLIC ${PROJECT_NAME} eigenpy::eigenpy)
  TARGET_LINK_BOOST_PYTHON(${PYTHON_LIB_NAME} PUBLIC)
  IF(BUILD_WITH_HPP_FCL_PYTHON_BINDINGS)
    TARGET_COMPILE_DEFINITIONS(${PYTHON_LIB_NAME} PRIVATE -DPINOCCHIO_PYTHON_INTERFACE_WITH_HPP_FCL_PYTHON_BINDINGS)
  ENDIF(BUILD_WITH_HPP_FCL_PYTHON_BINDINGS)
  IF(WIN32)
    TARGET_COMPILE_DEFINITIONS(${PYTHON_LIB_NAME} PRIVATE -DNOMINMAX)
    TARGET_LINK_LIBRARIES(${PYTHON_LIB_NAME} PUBLIC ${PYTHON_LIBRARY})
  ENDIF(WIN32)

  IF(NOT ${scalar_name} STREQUAL "default")
    MAKE_DIRECTORY("${${PROJECT_NAME}_BINARY_DIR}/bindings/python/${PROJECT_NAME}/${scalar_name}")
    SET(python_file "${${PROJECT_NAME}_SOURCE_DIR}/bindings/python/pinocchio/${scalar_name}/__init__.py")
    PYTHON_BUILD(${PROJECT_NAME}/${scalar_name} "__init__.py")
    INSTALL(FILES
      ${python_file}
      DESTINATION ${PINOCCHIO_PYTHON_INSTALL_DIR}/${scalar_name})

    # Append content to the main __init__.py
    FILE(APPEND ${MAIN_PYTHON_INIT} "from . import ${scalar_name}\n")
  ENDIF()

  SET_TARGET_PROPERTIES(${PYTHON_LIB_NAME}
    PROPERTIES
    PREFIX ""
    SUFFIX ${PYTHON_EXT_SUFFIX}
    OUTPUT_NAME "${PYTHON_LIB_NAME}"
    LIBRARY_OUTPUT_DIRECTORY "${CMAKE_BINARY_DIR}/bindings/python/${PROJECT_NAME}"
    )

  IF(UNIX AND NOT APPLE)
    SET_TARGET_PROPERTIES(${PYTHON_LIB_NAME} PROPERTIES INSTALL_RPATH "\$ORIGIN/../../..")
  ENDIF()

  INSTALL(
    TARGETS ${PYTHON_LIB_NAME}
    EXPORT ${TARGETS_EXPORT_NAME}
    DESTINATION ${PINOCCHIO_PYTHON_INSTALL_DIR}
    )

ENDFUNCTION()

IF(BUILD_PYTHON_INTERFACE)
  INCLUDE_DIRECTORIES(SYSTEM ${PYTHON_INCLUDE_DIRS})
  ADD_CUSTOM_TARGET(python)
  SET_TARGET_PROPERTIES(python PROPERTIES EXCLUDE_FROM_DEFAULT_BUILD True)

  SET(PKG_CONFIG_PYWRAP_REQUIRES "eigenpy >= 2.6.5")
  IF(IS_ABSOLUTE ${PYTHON_SITELIB})
    SET(ABSOLUTE_PYTHON_SITELIB ${PYTHON_SITELIB})
  ELSE()
    SET(ABSOLUTE_PYTHON_SITELIB ${CMAKE_INSTALL_PREFIX}/${PYTHON_SITELIB})
  ENDIF()
  SET(PINOCCHIO_PYTHON_INSTALL_DIR ${ABSOLUTE_PYTHON_SITELIB}/${PROJECT_NAME})

  # First generate the __init__.py file
  SET(MAIN_PYTHON_INIT "${CMAKE_CURRENT_BINARY_DIR}/pinocchio/__init__.py")
  CONFIGURE_FILE("${${PROJECT_NAME}_SOURCE_DIR}/bindings/python/pinocchio/__init__.py" ${MAIN_PYTHON_INIT})

  PINOCCHIO_PYTHON_BINDINGS_SPECIFIC_TYPE(default)
  SET(PYTHON_LIB_NAME "${PYWRAP}_default")

  IF(BUILD_WITH_AUTODIFF_SUPPORT)
    PINOCCHIO_PYTHON_BINDINGS_SPECIFIC_TYPE(cppad cppad)
    TARGET_COMPILE_DEFINITIONS(${PYWRAP}_cppad PRIVATE PYCPPAD_EXCLUDE_EIGEN_NUMTRAITS_SPECIALIZATION)
    TARGET_INCLUDE_DIRECTORIES(${PYWRAP}_cppad SYSTEM PUBLIC ${cppad_INCLUDE_DIR})
    TARGET_LINK_LIBRARIES(${PYWRAP}_cppad PUBLIC ${cppad_LIBRARY})
  ENDIF(BUILD_WITH_AUTODIFF_SUPPORT)

  IF(BUILD_WITH_CODEGEN_SUPPORT)
    PINOCCHIO_PYTHON_BINDINGS_SPECIFIC_TYPE(cppadcg cppadcg)
    TARGET_COMPILE_DEFINITIONS(${PYWRAP}_cppadcg PRIVATE PYCPPAD_EXCLUDE_EIGEN_NUMTRAITS_SPECIALIZATION)
    TARGET_INCLUDE_DIRECTORIES(${PYWRAP}_cppadcg SYSTEM PUBLIC ${cppadcg_INCLUDE_DIR})
    TARGET_LINK_LIBRARIES(${PYWRAP}_cppadcg PUBLIC ${cppadcg_LIBRARY} ${cppad_LIBRARY})
  ENDIF(BUILD_WITH_CODEGEN_SUPPORT)

  IF(BUILD_WITH_CASADI_SUPPORT)
    PINOCCHIO_PYTHON_BINDINGS_SPECIFIC_TYPE(casadi)
    TARGET_LINK_LIBRARIES(${PYWRAP}_casadi PUBLIC casadi)
  ENDIF(BUILD_WITH_CASADI_SUPPORT)

  IF(BUILD_PYTHON_BINDINGS_WITH_BOOST_MPFR_SUPPORT)
    find_package(GMP REQUIRED 6.0.0)
    find_package(MPFR REQUIRED 4.0.0)
    PINOCCHIO_PYTHON_BINDINGS_SPECIFIC_TYPE(mpfr)

    target_link_libraries(${PYWRAP}_mpfr PRIVATE SYSTEM gmp mpfr)
    SET_PROPERTY(TARGET ${PYWRAP}_mpfr PROPERTY CXX_STANDARD 11)
  ENDIF(BUILD_PYTHON_BINDINGS_WITH_BOOST_MPFR_SUPPORT)

  PYTHON_BUILD_FILE(${MAIN_PYTHON_INIT})
  INSTALL(FILES
      "${MAIN_PYTHON_INIT}c"
      DESTINATION ${PINOCCHIO_PYTHON_INSTALL_DIR}
    )

  # --- INSTALL SCRIPTS
  SET(PYTHON_FILES
    deprecated.py
    deprecation.py
    utils.py
    robot_wrapper.py
    romeo_wrapper.py
    explog.py
    shortcuts.py
    )

  FOREACH(python ${PYTHON_FILES})
    PYTHON_BUILD(${PROJECT_NAME} ${python})
    INSTALL(FILES
      "${${PROJECT_NAME}_SOURCE_DIR}/bindings/python/pinocchio/${python}"
      DESTINATION ${PINOCCHIO_PYTHON_INSTALL_DIR}
    )
  ENDFOREACH(python)

  # --- INSTALL VISUALIZATION SCRIPTS
  SET(PYTHON_VISUALIZE_FILES
    __init__.py
    base_visualizer.py
    gepetto_visualizer.py
    meshcat_visualizer.py
    panda3d_visualizer.py
    rviz_visualizer.py
    )

  FOREACH(python ${PYTHON_VISUALIZE_FILES})
    PYTHON_BUILD(${PROJECT_NAME}/visualize ${python})
    INSTALL(FILES
      "${${PROJECT_NAME}_SOURCE_DIR}/bindings/python/pinocchio/visualize/${python}"
      DESTINATION ${PINOCCHIO_PYTHON_INSTALL_DIR}/visualize)
  ENDFOREACH(python)

  # --- STUBS --- #
  IF(GENERATE_PYTHON_STUBS)
    LOAD_STUBGEN()
    GENERATE_STUBS(${CMAKE_CURRENT_BINARY_DIR} ${PROJECT_NAME} ${ABSOLUTE_PYTHON_SITELIB})
#    FIND_AND_REPLACE_STUBS(${CMAKE_CURRENT_BINARY_DIR} ${PROJECT_NAME} "__init__.pyi" "pinocchio.pinocchio_pywrap_default" "pinocchio")
  ENDIF(GENERATE_PYTHON_STUBS)

  # --- PACKAGING --- #

  # Format string
  SET(_PKG_CONFIG_PYWRAP_LIBDIR ${PINOCCHIO_PYTHON_INSTALL_DIR})
  SET(_PKG_CONFIG_PYWRAP_BINDIR ${PINOCCHIO_PYTHON_INSTALL_DIR})
  SET(_PKG_CONFIG_PYWRAP_CONFLICTS)
  SET(_PKG_CONFIG_PYWRAP_REQUIRES "${PROJECT_NAME}")
  FOREACH(dep ${PKG_CONFIG_PYWRAP_REQUIRES})
    SET(_PKG_CONFIG_PYWRAP_REQUIRES "${_PKG_CONFIG_PYWRAP_REQUIRES}, ${dep}")
  ENDFOREACH(dep ${PKG_CONFIG_PYWRAP_REQUIRES})

  SET(_PKG_CONFIG_PYWRAP_LIBS "-L\${libdir} -l${PYWRAP}")
  IF(APPLE)
    SET(_PKG_CONFIG_PYWRAP_LIBS "${_PKG_CONFIG_PYWRAP_LIBS} -Wl,-undefined,dynamic_lookup,${Boost_${UPPERCOMPONENT}_LIBRARY}")
  ELSE(APPLE)
    SET(_PKG_CONFIG_PYWRAP_LIBS "${_PKG_CONFIG_PYWRAP_LIBS} ${LIBINCL_KW}boost_python")
  ENDIF(APPLE)

  SET(_PKG_CONFIG_PYWRAP_CFLAGS "-I\${includedir}")
  SET(_PKG_CONFIG_PYWRAP_CFLAGS "${_PKG_CONFIG_PYWRAP_CFLAGS} -I${PYTHON_INCLUDE_DIRS}")
  FOREACH(cflags ${CFLAGS_DEPENDENCIES})
    SET(_PKG_CONFIG_PYWRAP_CFLAGS "${_PKG_CONFIG_PYWRAP_CFLAGS} ${cflags}")
  ENDFOREACH(cflags ${CFLAGS_DEPENDENCIES})

  CONFIGURE_FILE(
     "${CMAKE_CURRENT_SOURCE_DIR}/pinocchiopy.pc.cmake"
     "${CMAKE_CURRENT_BINARY_DIR}/pinocchiopy.pc")

  INSTALL(
      FILES "${CMAKE_CURRENT_BINARY_DIR}/pinocchiopy.pc"
      DESTINATION ${CMAKE_INSTALL_LIBDIR}/pkgconfig
      PERMISSIONS OWNER_READ GROUP_READ WORLD_READ OWNER_WRITE)

  IF(DOXYGEN_FOUND AND DOXYGEN_VERSION VERSION_GREATER 1.8.17)
    SET(DOXYGEN_GENERATE_HTML YES)
    SET(DOXYGEN_GENERATE_LATEX NO)
    SET(DOXYGEN_PROJECT_NAME "Pinocchio PyBind11 helpers.")
    CMAKE_POLICY(PUSH)
    CMAKE_POLICY(SET CMP0054 NEW)
    DOXYGEN_ADD_DOCS(doc_pybind11
      pybind11.hpp pybind11-all.hpp
      USE_STAMP_FILE
      COMMENT "Generating documentation of the PyBind11 helpers.")
    CMAKE_POLICY(POP)
  ENDIF()
ENDIF(BUILD_PYTHON_INTERFACE)
